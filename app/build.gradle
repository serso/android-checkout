/*
 * Copyright 2014 serso aka se.solovyev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Contact details
 *
 * Email: se.solovyev@gmail.com
 * Site:  http://se.solovyev.org
 */

apply plugin: 'com.android.application'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'
    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 24
        versionCode 26
        versionName '0.7.5'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            zipAlignEnabled true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:24.1.0'
    compile ('ch.acra:acra:4.7.0-RC.1') {
        exclude group: 'org.json'
    }
    compile project(':lib')
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    configurations.compile.each { File file -> classpath += project.files(file.path) }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives file: file('build/outputs/apk/checkout-release.apk'), name: 'checkout-app', classifier: 'signed', type: 'apk'
    archives file: file('build/outputs/mapping/release/mapping.txt'), name: 'checkout-app', classifier: 'proguard', type: 'txt'
}

signing {
    sign configurations.archives
}

group = "org.solovyev.android"
version = '0.7.5'

uploadArchives {
    doFirst {
        // for some reason Gradle :uploadArchives tries to upload null.txt.asc and null.map artifacts.
        // As we don't want those let's filter them now (note that these files are not included in the
        // list of project artifacts, so their origin is not clear)
        configurations.archives.artifacts.removeAll { artifact ->
            def removing = artifact.classifier == null && (artifact.type == 'txt.asc' || artifact.type == 'map')
            if (removing) {
                println "Removing artifacts: ${artifact.name}-${artifact.classifier}.${artifact.type}"
            }
            return removing
        }
    }
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                artifactId = 'checkout-app'
                name 'Android Checkout App'
                packaging 'apk'
                description 'Sample App for Android In-App Billing (Version 3)'
                url 'https://github.com/serso/android-checkout'

                scm {
                    url 'https://github.com/serso/android-checkout'
                    connection 'scm:https://serso@github.com/serso/android-checkout.git'
                    developerConnection 'scm:git://github.com/serso/android-checkout.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'se.solovyev'
                        name 'Sergey Solovyev'
                        email 'se.solovyev@gmail.com'
                    }
                }
            }
        }
    }
}